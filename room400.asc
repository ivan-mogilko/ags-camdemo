//////////////////////////////////////////////////////////////////////////////////////////
// Room 400: Magic Mirror
//----------------------------------------------------------------------------------------
//
// The player may acquire a magic mirror item in this room. This "mirror" works like an
// eye into another dimension. Not only you may see through, but even interaction through
// the mirror works.
// At some point player may teleport between dimensions. In this case the mirror will
// always display an opposite one: when we are in the first the mirror shows the second,
// when we are in the second - the mirror shows the first.
//
// This is implemented by having a room's background split into two parts, simulating two
// different places. Room scrolling is restricted, so there's no way player can scroll
// from one part to another and notice our little secret.
//
// The "mirror" is in fact the second viewport linked to the second camera. There's a
// GUI frame around it. Dragging this GUI will drag the viewport along, and change its
// camera's position too. This creates an illusion of exploring a parallel world through
// a porthole.
//
//////////////////////////////////////////////////////////////////////////////////////////

// Custom drag mode, used with DragDrop module
#define DRAGDROP_MIRROR_MODE 1

// Secondary viewport and camera, and their settings
Camera* LookCam;
Viewport* LookView;
#define CAM_WIDTH 234
#define CAM_HEIGHT 388
// MirrorView offsets define viewport's position relative
// to the GUI frame's position
int MirrorView_OffsetX, MirrorView_OffsetY;
// Camera2 offsets are used to keep this camera in the limits of the opposite room part,
// while moving it relatively along with the GUI frame on screen
int Camera2_OffsetX, Camera2_OffsetY;
// The mirror mask sprite is used to tell which parts of the frame GUI are transparent
// so that player could "click through" in the mirror viewport underneath. This is needed
// because AGS atm does not support pixel-perfect GUI detection.
// This is not the only possible solution. For instance, I could make a mirror frame with
// 4 GUIs around the viewport.
#define SPRITE_MIRRORMASK 3
DynamicSprite* MirrorMask;
DrawingSurface* MirrorMaskDS;


// Quest variables, help to recognize current game state
struct QuestS
{
	bool TakenKey;		// was the key taken
	bool OpenedDoor;	// was the door opened
	bool InRoom2;		// is the player currently in parallel dimension
};
QuestS Quest;


// Update mirror's viewport and camera positions,
// following GUI frame movement
void UpdateMirrorViewport()
{
	int x = gMagicMirror.X + MirrorView_OffsetX;
	int y = gMagicMirror.Y + MirrorView_OffsetY;
	LookView.X = x;
	LookView.Y = y;
	LookCam.X = x + Camera2_OffsetX;
	LookCam.Y = y + Camera2_OffsetY;
}

// Setup main camera position and mirror's camera offsets
// depending on which part of the room the player is in now.
void SetCameraLocation()
{
	if (Quest.InRoom2) {
		Game.Camera.SetAt(0, Screen.Height);
		Camera2_OffsetX = 0;
		Camera2_OffsetY = 0;
	} else {
		Game.Camera.SetAt(0, 0);
		Camera2_OffsetX = 0;
		Camera2_OffsetY = Screen.Height;
	}
	UpdateMirrorViewport();
}

// When the player drags the GUI frame we clamp the GUI position
// to the screen borders, and adjust the mirror's viewport and camera pos
void UpdateMirrorDrag()
{
	Point *p = Utils.ClampPosition(DragDrop.ObjectX, DragDrop.ObjectY, gMagicMirror.Width, gMagicMirror.Height, 
		0, 0, Screen.Width, Screen.Height);
	gMagicMirror.X = p.x;
	gMagicMirror.Y = p.y;
	UpdateMirrorViewport();
}

// Display / hide the mirror GUI and viewport
void ToggleMirror(bool on)
{
	gMagicMirror.Visible = on;
	LookView.Visible = on;
	UpdateMirrorViewport();
}


function room_Load()
{
	// Setup the room, initialize room's resources
	MirrorMask = DynamicSprite.CreateFromExistingSprite(SPRITE_MIRRORMASK);
	MirrorMaskDS = MirrorMask.GetDrawingSurface();
	
	LookCam = Camera.Create();
	LookCam.SetSize(CAM_WIDTH, CAM_HEIGHT);
	LookCam.SetAt(0, Screen.Height);
	LookView = Viewport.Create();
	LookView.SetPosition(0, 0, CAM_WIDTH, CAM_HEIGHT);
	LookView.Visible = false;
	LookView.Camera = LookCam;
	gMagicMirror.X = (Screen.Width - gMagicMirror.Width) / 2;
	gMagicMirror.Y = (Screen.Height - gMagicMirror.Height) / 2;
	gMagicMirror.Visible = false;
	MirrorView_OffsetX = (gMagicMirror.Width - CAM_WIDTH) / 2;
	MirrorView_OffsetY = (gMagicMirror.Height - CAM_HEIGHT) / 2;
	SetCameraLocation();
	
	gMenuButton.Visible = true;
	gInventory.Visible = true;

	Utils.RestrictCursorsTo(eModePointer);

	DragDrop.Enabled = true;
}

function room_Leave()
{
	// Clear player's inventory and delete all room's resources
	player.LoseInventory(i400Mirror);
	player.LoseInventory(i400Key);
	
	Utils.HideAllGUI();

	LookCam.Delete();
	LookView.Delete();
	MirrorMaskDS.Release();
	MirrorMask.Delete();
}

function room_AfterFadeIn()
{
}

function on_key_press(eKeyCode k)
{
	// If player has a mirror item, toggle the mirror on screen with a Space key
	if (player.HasInventory(i400Mirror) && (k == eKeySpace)) {
		ToggleMirror(!gMagicMirror.Visible);
	}
}

function on_mouse_click(MouseButton btn)
{
	// Don't let player interact with the room through the mirror border.
	// We keep mirror GUI non-clickable, otherwise it prevents from interacting
	// with the viewport underneath it's translucent part.
	// But this also means that clicking on the mirror's border (GUI) will click
	// through, and we don't want that to happen.
	if (gMagicMirror.Visible) {
		bool clicked_on_solid_border = false;
		gMagicMirror.Clickable = true; // BECAUSE IT'S AGS!!!
		if (GUI.GetAtScreenXY(mouse.x, mouse.y) == gMagicMirror) {
			if (MirrorMaskDS.GetPixel(mouse.x - gMagicMirror.X, mouse.y - gMagicMirror.Y) == 0) {
				clicked_on_solid_border = true;
			}
		}
		gMagicMirror.Clickable = false;
		if (clicked_on_solid_border) {
			ClaimEvent(); // intercept this click
			return;
		}
	}
}

function on_call(int value)
{
	// Handle custom room events
	if (value == ROOM_EVENT__ITEM_USED) {
		// Inventory item was activated
		switch (inventory[game.inv_activated]) {
			case i400Mirror:
				ToggleMirror(!gMagicMirror.Visible);
				break;
			case i400Key:
				Display("You've got a key. Now to find a lock.");
				break;
		}
	}
}

function late_repeatedly_execute_always()
{
	if (IsGamePaused()) {
		return;
	}

	// Handle mirror's drag'n'drop mechanic;
	// EvtWantObject - looking for a draggable object under the cursor
	if (DragDrop.EvtWantObject) {
		if (gMagicMirror.Visible) {
			gMagicMirror.Clickable = true; // BECAUSE IT'S AGS!!!
			if (GUI.GetAtScreenXY(mouse.x, mouse.y) == gMagicMirror) {
				if (MirrorMaskDS.GetPixel(mouse.x - gMagicMirror.X, mouse.y - gMagicMirror.Y) == 0) {
					// non-transparent GUI pixel under the cursor:
					// hook up the mirror for dragging
					DragDrop.HookObject(DRAGDROP_MIRROR_MODE, gMagicMirror.X, gMagicMirror.Y);
				}
			}
			gMagicMirror.Clickable = false;
		}
	// IsDragging - in process of drag
	} else if (DragDrop.IsDragging) {
		UpdateMirrorDrag(); // update the mirror position
	}
}

// Interacting with a chest
function oChest_Interact()
{
	if (player.HasInventory(i400Mirror)) {
		Display("The chest is now empty.");
	} else {
		player.AddInventory(i400Mirror);
		oChest.Graphic = 8;
		Display("Inside the chest you find a magic mirror.\n\nInteract with it to see what it reveals. Then drag it around and investigate a parallel dimension!");
	}
}

// Interacting with a key object (hanging on a wall hook)
function oKey_Interact()
{
	if (Quest.TakenKey) {
		Display("There's nothing of interest there.");
	} else {
		player.AddInventory(i400Key);
		Quest.TakenKey = true;
		oKey.Graphic = 11;
		Display("You pick up the key.");
	}
}

// Interacting with a door inside the second dimension
function oDoor1_Interact()
{
	if (Quest.OpenedDoor) {
		Display("The door is now open.");
	} else if (player.HasInventory(i400Key)) {
		player.LoseInventory(i400Key);
		Quest.OpenedDoor = true;
		oDoor1.SetView(VOPENEDDOOR, 0, 0);
		oDoor1.X = 190;
		oDoor1.Y = 1330;
		oPortal.Visible = true;
		Display("You unlocked the door. Or... is it more than just a door?");
	} else {
		Display("The door is locked and you don't have any keys.");
	}
}

// Interacting with a portal in the first dimension
function hPortal1_Interact()
{
	if (Quest.InRoom2) {
		Quest.InRoom2 = false;
		SetCameraLocation();
		Display("Woooosh!");
	} else {
		Display("You cannot travel through the mirror, you have to walk by yourself.");
	}
}

// Interacting with a portal in the second dimension
function oPortal_Interact()
{
	if (!Quest.InRoom2) {
		Quest.InRoom2 = true;
		SetCameraLocation();
		Display("Woooosh!");
	} else {
		Display("You cannot travel through the mirror, you have to walk by yourself.");
	}
}
